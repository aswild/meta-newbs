# NEWBS distro configuration

##### Yocto Configuration #####

DISTRO_VERSION = "NEWBS 0.5"

# don't append -glibc to TMPDIR
TCLIBCAPPEND = ""

##### Kernel stuff #####
PREFERRED_PROVIDER_virtual/kernel = "linux-raspberrypi-newbs"
#PREFERRED_PROVIDER_virtual/kernel = "linux-raspberrypi"
#PREFERRED_VERSION_linux-raspberrypi-newbs = "4.8%"
PREFERRED_VERSION_linux-raspberrypi-newbs = "4.4%"
PREFERRED_VERSION_linux-raspberrypi = "4.4%"
KERNEL_IMAGETYPE = "Image"
DTB_DEPLOYDIR = "dtb"

# kernel commandline
NEWBS_ROOT ?= "/dev/mmcblk0p2"
NEWBS_ROOT_TYPE ?= "squashfs-xz"
CMDLINE = "dwc_otg.lpm_enable=0 console=serial0,115200 console=tty1 root=${NEWBS_ROOT} rootwait earlyprintk"
CMDLINE_append_debug = " debug"

# remove unneeded DTB overlays
KERNEL_DEVICETREE_remove = " \
    overlays/hifiberry-amp.dtbo \
    overlays/hifiberry-dac.dtbo \
    overlays/hifiberry-dacplus.dtbo \
    overlays/hifiberry-digi.dtbo \
    overlays/iqaudio-dac.dtbo \
    overlays/iqaudio-dacplus.dtbo \
    overlays/pitft22.dtbo \
    overlays/pitft28-resistive.dtbo \
"

KERNEL_DEVICETREE_append = " \
    overlays/pi3-spi-shiftbrite.dtbo \
"

# Wifi firmware
ALTERNATIVE_PRIORITY_linux-firmware-bcm43430 = "50"

##### Init stuff #####
NEWBS_INIT = "newbs-init-image"
INITRAMFS_FSTYPES = "cpio.xz"
INITRAMFS_LOAD_ADDR = "0x00c00000"
NEWBS_INIT_DEST = "newbs-init.${INITRAMFS_FSTYPES}"


##### Root FS stuff #####
DISTRO_FEATURES += " \
    ipv6 \
    doc-pkgs \
"

DISTRO_FEATURES_remove = "x11"
DISTRO_FEATURES_BACKFILL_CONSIDERED = "sysvinit pulseaudio"

# locales
GLIBC_GENERATE_LOCALES = "en_US.UTF-8"
IMAGE_LINGUAS = "en-us"
DISTRO_FEATURES += "libc-locales libc-charsets libc-locale-code"

# systemd
DISTRO_FEATURES += "systemd"
VIRTUAL-RUNTIME_init_manager = "systemd"

DISTROOVERRIDES .= "${@':hostpython3' if d.getVar('HOST_DEFAULT_PYTHON3',True)=='y' else ''}"

##### Image stuff #####
IMAGE_CLASSES = "newbs-bootimg"
IMAGE_FEATURES += "read-only-rootfs"
IMAGE_FSTYPES = "ext4 squashfs-xz tar.xz newbs-bootimg"
