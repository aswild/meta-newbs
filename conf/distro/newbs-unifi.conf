# NEWBS UniFi Controller distro config

require conf/distro/include/newbs-base.inc

DISTRO_NAME = "NEWBS UniFi"
DISTRO_VERSION = "1.2"
DISTRO_CODENAME = "${LAYERSERIES_COMPAT_newbs}"

DISTRO_FEATURES_BACKFILL_CONSIDERED += "pulseaudio"

# disable empty root password
IMAGE_FEATURES:remove = "debug-tweaks"

# maximize CPU memory on Pi3 since we don't use the GPU
# and the Unifi controller is pretty memory-hungry
EXTRA_CONFIG_TXT:append:raspberrypi3 = "\
gpu_mem=16\n\
start_file=start_cd.elf\n\
fixup_file=fixup_cd.dat"

# Disable x11 in Java, we only need headless. Target Java is already implicitly headless because
# target DISTRO_FEATURES doesn't include x11 for newbs.
PACKAGECONFIG:remove:pn-openjdk-8-native = "x11"
PACKAGECONFIG:remove:pn-icedtea7-native = "x11"
# these icedtea deps should be PACKAGECONFIG based, but are also included unconditionally
DEPENDS:remove:pn-icedtea7-native = "libx11-native xorgproto-native"

# Stick with mongodb 4.4 - it builds much faster and is half the binary size than mongodb 7.0
PREFERRED_PROVIDER_mongodb = "mongodb"
